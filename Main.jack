class Main {
    static boolean playing, winner;

    function void main() {
        var Board board;
        var Controller controller;
        var Piece selectedPiece;
        var boolean emptySquare, invalidMove, escaped;

        let board = Board.new();
        let playing = true;
        let controller = Controller.new();
        do Sprites.init();
        do Display.setMessages();

        while(playing) {
            do Display.selectSquare(0);
            do Display.drawBoard(board);

            let emptySquare = true;
            let invalidMove = true;
            let escaped = false;
            

            while(emptySquare & ~escaped) {

                while(~controller.getInput(board)) {}

                let escaped = controller.getEscaped();

                if(~escaped) {
                    let selectedPiece = controller.selectPiece(board);
                    do Display.selectSquare(selectedPiece);
                    do Display.drawBoard(board);
                }

                if(selectedPiece > 0) { 
                    let emptySquare = false;
                }

                do controller.clearInputs();

            }


            if(~escaped) {do Display.highlightSelectedSquare();}

            while(invalidMove & ~escaped) {
                while(~controller.getInput(board)) {}

                let escaped = controller.getEscaped();
                if(~escaped) {
                    do Display.drawBoard(board);
                    let invalidMove = controller.movePiece(board, selectedPiece);
                }

                do controller.clearInputs();
            }

        }

        do Screen.clearScreen();

        if(winner) {
            do Output.printString("Checkmate! Black Wins!");
        }
        else {
            do Output.printString("Checkmate! White Wins!");
        }

        return;
    }

    function void endGame(boolean winner) {
        let playing = false;
        return;
    }
}